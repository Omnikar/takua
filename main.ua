# Experimental!
I ~ "git: github.com/Marcos-cat/iris"
~ "game"
  ~ Board Move Process
  ~ Cap Flat Wall
~ "render"
  ~ Player₁ Player₂
  ~ Scene

T‼ ← &p $"_ took _" ^0 ⍜now^1

VoxelScale ← 2.5
WinSize    ← 700_700

Code ← ⌅(+@A⊥26|⌝⊥26-@A⌵)
# Normalized spherical to rectangular coordinates
SphToRect ← ˜⊂×⍜°⊟∩°∠

# Render voxels and rescale to screen size
# ? Voxels CamPos
MkVoxels ← ▽₂⊢÷△₂⊸⊙WinSize voxels (Scale:VoxelScale) ⊙(Camera:SphToRect)
Message  ← I~Draw~Text White 30 100_100

┌─╴GameMode
  |Local
  |Online {
    MyColor
    OtherPort
    Tunnel
  }
└─╴

~GameState {
  ClickMap
  Texture
  Move
  Board
  GameMode
  CamPos ← [π/4 ∠√2 1]
}

RedrawScene ← (
  Scene⊸⊃GameState~Move GameState~Board

  ⍣(◡⋅GameState~GameMode
    ⍣(°GameMode~Local
      °1◿₂Board~Turn◡⋅GameState~Board
    | °1GameMode~Online~MyColor)
    ⍜⊃Scene~Hover Scene~State∩¯
  | )

  ∩⌟MkVoxels Scene~IdxsAndBuild ⊙⊸GameState~CamPos
  GameState!°⊸⊃(ClickMap|I~Texture~Image Texture)
)

HandleCamRotation ← (
  ⊸≠₀/+I~Key~ULDR 0
  ⨬◌(×I~Window~Dt⊟¯°ℂ
     ⍜⊙GameState~CamPos-
     RedrawScene)
)

HandleBoardClick ← I~Mouse!Pressed‼Left(
  ⊙⊸GameState~ClickMap
  ⊸±⊡⌊×◡⋅△÷WinSize˜⊟°ℂ
  ⨬◌(
    ⊙⊸⊃(♭₂⇡△◡⋅Board~State ⊃GameState~Move GameState~Board)
    ⊏-₁
    Move~ClickSquare
  
    # Opponents place each other's flats on the first two moves
    ⍣(°1<2◡⋅Board~Turn
      ⍣(°⊸Move~Place~Piece ¯1|⋅Move~Uninit⍜°Move~Move∘)
    | )
  
    °⊸⊃GameState~Move GameState~Board
    RedrawScene
  )
)

HandleMoveSubmission ← ⍣(
  ⊸⊃GameState~Move GameState~Board
  Process~Validate
  I~Draw~Text White 40 -50_60 WinSize ">"
  I~Key!Pressed‼(
    {Space @\n}
  | ⍣(◡⊙⋅(GameMode~Online~Tunnel GameState~GameMode)
      I~Net~Give ⍣⍜Move~Place~Piece¯∘
    | )
    Process
    ⍣(◡⋅GameState~GameMode
      °GameMode~Local
      ⍜Board~State¯
    | )
    Move
  )
  °⊸⊃GameState~Move GameState~Board
| )

HandleWinState ← (
  ⟜(°1⊸/↥Board~CheckWin) ⊸GameState~Board
  ⍣(◡⋅⋅GameState~GameMode◿₂Board~Turn
    =GameMode~Online~MyColor
  )⋅1
  # Bias for the active player in case of a double road
  ⍜⊏(+0.1)
  ⊢⍖
  ⍣(◡⋅⊃GameState~GameMode GameState~Board
    °GameMode~Local
    ≠◿₂Board~Turn
    {"White wins!" "Black wins!"}
  | {"You win!" "You lose!"})
  Message˜⊏
)

HandleActivePlayer ← (
  ⊸GameState~GameMode
  ⍣°GameMode~Local(
    ◌°.◿₂Board~Turn◡⋅GameState~Board GameMode~Online~MyColor
  )
  HandleBoardClick
  HandleMoveSubmission
)

HandleMultiplayerWait ← (
  ⊸GameState~GameMode
  GameMode~Online~Tunnel
  Message "Waiting for opponent..."
  I~Net~Get!(
    °⊸GameState~Move
    ⍜⊃(GameState~Move|GameState~Board)(Move Process)
    RedrawScene
  )
)

DrawPieceCounters ← (
  ⊸⊃(
    GameState~GameMode
  | Board!⊃(◿₂Turn|State|NFlats|NCaps)GameState~Board)
  ⍥¯×≍GameMode~Local
  ⟜¯
  ∩⌟₂(
    ↥₀
    /◇⊂♭
    ⊃(∊Flat_Wall|=Cap)
    ˜∩-∩/+
  )
  °⋕⊟₄
  [20_20 20_60 [-55⊢WinSize 20] [-55⊢WinSize 60]]
  ▽2Player₁_Player₂ 40
  I~Draw~Text
)

⍢◌(&pf "[l]ocal or [o]nline? "
   ¬⊃∊˜⊗"lo"⊢&sc)
⨬GameMode~Local(
  I~Net~Start LocalPort:+⚂₁₀₀ 8080
  ⊸I~Net~Port
  ⟜(&p $"Your code is _" Code)
  &pf "Enter opponent's code: "
  °Code &sc
  ⊃>⟜𝄐I~Net~Connect
  GameMode~Online
)

Board 5
Move

I~Open WinSize "Tak"
I~Window!°FPS 30

⊙I~Texture~LoadImage ∩⌟↯ ⟜⊂WinSize 4 0

GameState

RedrawScene

I~Loop!(
  HandleCamRotation

  ⍣(HandleWinState
  | HandleActivePlayer
  | HandleMultiplayerWait)

  I~Draw~Background ↯4 0
  I~Draw~Background ⊸GameState~Texture

  DrawPieceCounters
)

⊃GameState~GameMode(⊂Board!⊃History State GameState~Board)
≡&s ⍥(⍥¯◿₂°⊏) ≍GameMode~Local
